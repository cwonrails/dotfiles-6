# vim:ft=zsh ts=2 sw=2 sts=2

CURRENT_BG='NONE'
PRIMARY_FG=black

# Characters
SEGMENT_SEPARATOR="\ue0b0"
PLUSMINUS="\u00b1"
BRANCH="\ue0a0"
DETACHED="\u27a6"
CROSS="\u2718"
LIGHTNING="\u26a1"
GEAR="\u2699"

# Begin a segment
# Takes two arguments, background and foreground. Both can be omitted,
# rendering default background/foreground.
prompt_segment() {
  local bg fg
  [[ -n $1 ]] && bg="%K{$1}" || bg="%k"
  [[ -n $2 ]] && fg="%F{$2}" || fg="%f"
  if [[ $CURRENT_BG != 'NONE' && $1 != $CURRENT_BG ]]; then
    print -n "%{$bg%F{$CURRENT_BG}%}$SEGMENT_SEPARATOR%{$fg%}"
  else
    print -n "%{$bg%}%{$fg%}"
  fi
  CURRENT_BG=$1
  [[ -n $3 ]] && print -n $3
}

# End the prompt, closing any open segments
prompt_end() {
  if [[ -n $CURRENT_BG ]]; then
    print -n "%{%k%F{$CURRENT_BG}%}$SEGMENT_SEPARATOR"
  else
    print -n "%{%k%}"
  fi
  print -n "%{%f%}"
  CURRENT_BG=''
}

### Prompt components
# Each component will draw itself, and hide itself if no information needs to be shown

# Context: user@hostname (who am I and where am I)
prompt_context() {
  if [[ -n "$SSH_CONNECTION" ]]; then
    prompt_segment white $PRIMARY_FG " %n@%m "
  fi
}

# Git: branch/detached head, dirty status
prompt_git() {
  local color ref
  is_dirty() {
    test -n "$(git status --porcelain --ignore-submodules)"
  }
  ref="$vcs_info_msg_0_"
  if [[ -n "$ref" ]]; then
    if is_dirty; then
      color=yellow
      ref="${ref} $CROSS "
    else
      color=green
      ref="${ref} "
    fi
    if [[ "${ref/.../}" == "$ref" ]]; then
      ref="$BRANCH $ref"
    else
      ref="$DETACHED ${ref/.../}"
    fi
    prompt_segment $color $PRIMARY_FG
    print -Pn " $ref"
  fi
}

# Dir: current working directory
prompt_dir() {
  prompt_segment blue $PRIMARY_FG ' %~ '
}

# Status:
# - was there an error
# - am I root
# - are there background jobs?
prompt_status() {
  local symbols
  symbols=()
  [[ $UID -eq 0 ]] && symbols+="%{%F{yellow}%}$LIGHTNING"
  [[ $(jobs -l | wc -l) -gt 0 ]] && symbols+="%{%F{cyan}%}$GEAR"

  [[ -n "$symbols" ]] && prompt_segment $PRIMARY_FG default " $symbols "
}

# Display current time
prompt_time() {
  prompt_segment $PRIMARY_FG white
  print -Pn " %* "
}

prompt_human_time_to_var() {
  local human=" " total_seconds=$1 var=$2
  local days=$(( total_seconds / 60 / 60 / 24 ))
  local hours=$(( total_seconds / 60 / 60 % 24 ))
  local minutes=$(( total_seconds / 60 % 60 ))
  local seconds=$(( total_seconds % 60 ))
  (( days > 0 )) && human+="${days}d "
  (( hours > 0 )) && human+="${hours}h "
  (( minutes > 0 )) && human+="${minutes}m "
  human+="${seconds}s"

  # store human readable time in variable as specified by caller
  typeset -g "${var}"="${human}"
}

prompt_maximbaz_check_cmd_exec_time() {
  integer elapsed
  (( elapsed = SECONDS - ${prompt_maximbaz_cmd_timestamp:-$SECONDS} ))
  prompt_maximbaz_cmd_exec_time=
  (( elapsed >= 5 )) && {
    prompt_human_time_to_var $elapsed "prompt_maximbaz_cmd_exec_time"
  }
  unset prompt_maximbaz_cmd_timestamp
}

## Main prompt
prompt_maximbaz_main() {
  CURRENT_BG='NONE'
  prompt_time
  prompt_status
  prompt_context
  prompt_dir
  prompt_git
  prompt_end
}

prompt_maximbaz_precmd() {
  prompt_maximbaz_check_cmd_exec_time

  vcs_info

  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
  RPROMPT='%F{yellow}${prompt_maximbaz_cmd_exec_time}%f'
  PROMPT='
%{%f%b%k%}$(prompt_maximbaz_main)
%B%(?.%F{blue}.%F{red})‚ùØ%f%b  ${editor_info[keymap]}'
}

prompt_maximbaz_preexec() {
  prompt_maximbaz_cmd_timestamp=$SECONDS
}

prompt_maximbaz_setup() {
  # Don't display percentages when newline is missing
  export PROMPT_EOL_MARK=''

  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  prompt_opts=(cr subst percent)

  add-zsh-hook precmd prompt_maximbaz_precmd
  add-zsh-hook preexec prompt_maximbaz_preexec

  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'

  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*' check-for-changes false
  zstyle ':vcs_info:git*' formats '%b'
  zstyle ':vcs_info:git*' actionformats '%b (%a)'
}

prompt_maximbaz_setup "$@"
